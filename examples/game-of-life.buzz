import "std";
import "io";
import "examples/sdl-wrapped" as _;

/// buzz -L/path/to/SDL2.dylib/so/dll examples/game-of-life.buzz
object World {
    width: int,
    height: int,
    cells: mut [bool],

    static fun init(width: int, height: int) > mut World {
        final world = mut World{
            width,
            height,
            cells = mut [],
        };

        for (i: int = 0; i < width * height; i = i + 1) {
            world.cells.append(std\random(max: 5) == 1);
        }

        return world;
    }

    fun neighbors(x: int, y: int) > int {
        var liveCount = 0;
        for (dy: int = y - 1; dy <= y + 1; dy = dy + 1) {
            for (dx: int = x - 1; dx <= x + 1; dx = dx + 1) {
                if (dy > 0 and dy > 0 and dx < this.width and dy < this.height and this.cells[dy * this.width + dx]) {
                    liveCount = liveCount + 1;
                }
            }
        }

        return liveCount - if (this.cells[y * this.width + x])
            1
        else
            0;
    }

    mut fun step() > void {
        final cells: mut [bool] = mut [];
        for (y: int = 0; y < this.height; y = y + 1) {
            for (x: int = 0; x < this.width; x = x + 1) {
                final coordinate = y * this.width + x;
                final liveNeighbors = this.neighbors(x: x, y: y);

                cells.append(liveNeighbors == 3 or (this.cells[coordinate] and liveNeighbors == 2));
            }
        }

        this.cells = cells;
    }

    fun dump() > void {
        for (y: int = 0; y < this.height; y = y + 1) {
            for (x: int = 0; x < this.width; x = x + 1) {
                io\stdout.write(if (this.cells[y * this.width + x])
                    "x"
                else
                    ".") catch void;
            }
            io\stdout.write("\n") catch void;
        }
    }

    fun draw(renderer: Renderer) > void !> SDLError {
        final cellWidth = 800 / this.width;
        final cellHeight = 600 / this.height;

        for (y: int = 0; y < this.height; y = y + 1) {
            for (x: int = 0; x < this.width; x = x + 1) {
                renderer.setRenderDrawColor(
                    if (this.cells[y * this.width + x])
                        Color{
                            red = 0x01,
                            green = 0x9d,
                            blue = 0xe0,
                        }
                    else
                    Color{
                        red = 0xff,
                        green = 0xff,
                        blue = 0xff,
                    },
                );

                renderer.fillRect(
                    Rect{
                        x = cellWidth * x,
                        y = cellHeight * y,
                        width = cellWidth,
                        height = cellHeight,
                    },
                );
            }
        }

        renderer.setRenderTarget(null);
        renderer.setRenderDrawColor(
            Color{
                red = 0,
                green = 0,
                blue = 0,
            },
        );
    }
}

fun loop(world: mut World, sdl: SDL, renderer: Renderer, texture: Texture) > void !> SDLError {
    var dt = sdl.ticks();
    while (true) {
        if (sdl.pollEvent() -> event) {
            if (event.@"type" == EventType.Quit.value) {
                break;
            }
        }

        if (sdl.ticks() - dt > 300) {
            dt = sdl.ticks();
            renderer.setRenderTarget(texture);
            world.draw(renderer);
            renderer.setRenderTarget(null);

            world.step();
        }

        renderer.renderCopy(texture);

        renderer.renderPresent();

        sdl.delay(100.0);
    }
}

fun main(args: [str]) > void !> SDLError {
    final sdl = SDL.init([ Subsystem.Video ]);

    final window = Window.init(
        "buzz example: Game of Life",
        width: 800,
        height: 600,
        flags: [ WindowFlag.OpenGL, WindowFlag.AllowHighDPI ],
    );

    final renderer = window.createRenderer(
        index: -1,
        flags: [ RendererFlag.Accelerated, RendererFlag.TargetTexture ],
    );

    final texture = renderer.createTexture(
        format: PixelFormat.RGBA8888,
        access: TextureAccess.Target,
        width: 800,
        height: 600,
    );

    final width = if (args.len() > 0)
        std\parseInt(args[0])
    else
        null;
    final height = if (args.len() > 1)
        std\parseInt(args[1])
    else
        null;

    final world = World.init(
        width: width ?? 10,
        height: height ?? 10,
    );

    loop(
        world,
        sdl,
        texture,
        renderer,
    );

    sdl.quit();
}
