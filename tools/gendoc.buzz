import "std";
import "debug";
import "serialize";
import "io";
import "fs";
import "errors";
import "buffer";

object Declaration {
    str declarationType,
    str? name = null,
    str typeDef,
    str? docblock,

    {str, Declaration} subDeclarations,

    static fun init(
        str declarationType,
        str typeDef,
        str? docblock,
        {str, Declaration}? subDeclarations
    ) > Declaration -> Declaration{
        declarationType = declarationType,
        typeDef = typeDef,
        docblock = docblock,
        subDeclarations = subDeclarations ?? {<str, Declaration>},
    }

    | Split docblock by @ instructions
    static fun handleDocblock(str docblock) > {str, str} {
        const pat atPattern = $"@([^@ ]+)(\s+[^\n@]+)?";
        {str, str} out = {<str, str>};

        foreach (str line in docblock.split("\\n")) {
            if (atPattern.match(line) -> at) {
                if (at.len() > 2) {
                    out[at[1].trim()] = at[2];
                } else {
                    out[at[1].trim()] = "";
                }
            } else {
                out["unspecified"] = (out["unspecified"] ?? "") + line;
            }
        }

        return out;
    }

    fun toMarkdown(str? heading, Buffer out) > void !> WriteWhileReadingError {
        const pat atParamPattern = $"([^\s]+)\s+(.+)";

        if (this.docblock == null) {
            return;
        }

        {str, str} processed = Declaration.handleDocblock(this.docblock ?? "");

        | Ignore things marked with @private
        if (processed["private"] != null) {
            return;
        }

        out.write("\n{heading ?? "###"} `{this.typeDef} {this.name ?? ""}`\n");

        out.write(processed["unspecified"] ?? "");

        foreach (str instruction, str content in processed) {
            if (instruction == "param") {
                [str]? param = atParamPattern.match(content);

                out.write("\n- ");
                if (param != null) {
                    out.write("**`{param![1]}`:** {param![2]}\n");
                } else {
                    out.write("{content}\n");
                }
            } else if (instruction == "return") {
                out.write("\n\n**Returns:** {content}");
            }
        }
    }
}

fun extractDeclarations([Boxed] statements) > [Declaration] !> JsonParseError {
    [Declaration] declarations = [<Declaration>];

    foreach (Boxed statement in statements) {
        {str, Boxed} statementMap = statement.mapValue();
        str nodeType = statement.q(["node"]).stringValue();

        if (
            nodeType == "Function"
            or nodeType == "VarDeclaration"
            or nodeType == "FunDeclaration"
            or nodeType == "Enum"
            or nodeType == "ObjectDeclaration"
        ) {
            Declaration declaration = Declaration.init(
                declarationType: nodeType,
                typeDef: statement.q(["type_def"]).stringValue(),
                docblock: statement.q(["docblock"]).string(),
            );

            if (nodeType == "VarDeclaration") {
                declaration.name = statement.q(["name"]).string();
            }

            if (nodeType == "ObjectDeclaration") {
                {str, Boxed} props = statement.q(["members"]).mapValue();
                foreach (str name, Boxed member in props) {
                    str typeDef = member.q(["type_def"]).stringValue();
                    str displayName = "{typeDef} {name}";
                    if (typeDef.indexOf("fun") == 0) {
                        displayName = typeDef;
                    }

                    declaration.subDeclarations[name] =
                        Declaration.init(
                            declarationType: "member",
                            typeDef: displayName,
                            docblock: member.mapValue()["docblock"]?.string(),
                        );
                }
            }

            declarations.append(declaration);
        }
    }

    return declarations;
}

fun getDeclarations(str path) > [Declaration] 
    !> FileSystemError,
       UnexpectedError,
       ReadWriteError,
       CompileError,
       JsonParseError,
       WriteWhileReadingError {
    File file = File.open(path, mode: FileMode.read);

    str source = ast(
        file.readAll(),
        scriptName: path.sub(0, len: path.indexOf(".buzz")!)
    );
    file.close();

    Boxed? root = null;

    try {
        root = jsonDecode(source);
    } catch (JsonParseError error) {
        print("Could not decode ast of for {path}");

        return [<Declaration>];
    }

    | Root must be a function
    assert(root?.q(["node"]).string() ?? "" == "Function", message: "Expected root node to be a function");

    return extractDeclarations(root?.q(["body", "statements"]).listValue() ?? [<Boxed>]);
}

fun genMarkdownDoc([Declaration] declarations, Buffer out) > void !> WriteWhileReadingError {
    foreach (Declaration decl in declarations) {
        decl.toMarkdown("###", out: out);
    }
}

fun main([str] args) > void
    !> FileSystemError,
       ReadWriteError,
       InvalidArgumentError,
       UnexpectedError,
       CompileError,
       JsonParseError,
       WriteWhileReadingError
{
    {str, str} markdownDoc = {<str, str>};
    foreach (str file in list("src/lib")) {
        if (file.endsWith(".buzz")) {
            print("Generating doc for {file}...");
            [Declaration] declarations = getDeclarations("src/lib/{file}");

            if (declarations.len() > 0) {
                Buffer out = Buffer.init();

                genMarkdownDoc(declarations, out: out);
                markdownDoc[file] = out.toString();
            }
        }
    }

    File mdFile = File.open("{currentDirectory()}/doc/std.md", mode: FileMode.write);

    mdFile.write("# Buzz std lib\n## Table of contents\n");
    foreach (str lib, str libDoc in markdownDoc) {
        const str libName = lib.sub(0, len: lib.indexOf(".buzz")!);
        mdFile.write("\n- [{libName}](#{libName.replace(" ", with: "-")})");
    }

    foreach (str lib, str libDoc in markdownDoc) {
        const str libName = lib.sub(0, len: lib.indexOf(".buzz")!);
        mdFile.write("\n## {libName}\n{libDoc}");
    }

    mdFile.close();
}