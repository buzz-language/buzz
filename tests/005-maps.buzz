import "std";

test "Maps" {
    {str: int} map = {
        "hello": 1,
        "bye": 2,
    };

    _ = {1: true, 2: false};

    assert(map["bye"] is int?, message: "yeah");

    assert(map["bye"] == 2, message: "map subscript");
    assert(({1: true, 2: false})[2] == false, message: "map expression subscript");

    assert(map.remove("hello") == 1, message: "removed element");
    | assert(map["hello"] == null, message: "removed element");
    assert(map.size() == 1, message: "map size");
}

test "map merge" {
    {str: int} map = {"one": 1, "two": 22} + {"three": 3, "two": 2};

    assert(map["two"] == 2, message: "map merge");
    assert(map.size() == 3, message: "map merge");
}

test "map.keys" {
    assert({"one": 1, "two": 2, "three": 3}.keys().join(",") == "one,two,three", message: "map.keys");
}

test "map.values" {
    assert({"one": 1, "two": 2, "three": 3}.values().join(",") == "1,2,3", message: "map.values");

    assert({<str: str>}.keys().len() == 0, message: "yo empty map");
}

test "map.diff" {
    {str: int} first = {
        "one": 1,
        "two": 2,
        "three": 3,
    };

    {str: int} second = {
        "two": 22,
        "three": 33,
        "four": 4,
    };

    {str: int} diff = first.diff(second);

    assert(diff.size() == 1 and diff["one"] != null, message: "Could use map.diff");
}

test "map.intersect" {
    {str: int} first = {
        "one": 1,
        "two": 2,
        "five": 5,
    };

    {str: int} second = {
        "two": 22,
        "three": 33,
        "four": 4,
    };

    {str: int} intersect = first.intersect(second);

    assert(intersect.size() == 1 and intersect["two"] != null, message: "Could use map.intersect");
}

test "map.clone" {
    {str: int} first = {
        "one": 1,
        "two": 2,
        "five": 5,
    };
    {str: int} copy = first.clone();

    assert(copy.size() == first.size(), message: "Could clone map");
    foreach (str key, int value in copy) {
        assert(first[key] == value, message: "Could clone map");
    }
}

test "empty map type inferring" {
    var map = {};

    assert(typeof map == <{any: any}>);

    {str: int} smap = {};

    assert(typeof smap == <{str: int}>);
}