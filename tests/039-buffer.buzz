import "lib/std";
import "lib/buffer";

test "Reading and writing in a buffer" {
    Buffer buffer = Buffer.init();

    buffer.writeInt("hello world".len());
    buffer.write("hello world");
    buffer.writeFloat(1238.324);
    buffer.writeBoolean(true);

    [int] expected = [
        11, 0, 0, 0,                                          | "hello world".len()
        104, 101, 108, 108, 111, 32, 119, 111, 114, 108, 100, | "hello world"
        158, 239, 167, 198, 75, 89, 147, 64,                  | 1238.324
        1,                                                    | true
    ];
    foreach (int i, str char in buffer.toString()) {
        assert(expected[i] == char.byte(0), message: "Buffer has expected content");
    }

    assert(buffer.len() == expected.len(), message: "wrote expected number of bytes");
    
    const int len = buffer.readInt() ?? -1;
    assert(len == 11, message: "could read number");

    assert(buffer.read(len) == "hello world", message: "could read n bytes");
    assert(buffer.readFloat() == 1238.324, message: "could read float");
    assert(buffer.readBoolean() == true, message: "could read boolean");

    buffer.empty();

    assert(buffer.len() == 0, message: "could empty buffer");

    buffer.deinit();
}