import "lib/std";
import "lib/math" as math;
import "lib/errors";

object Node {
    Node? left = null,
    Node? right = null,
}

fun bottomUpTree(int depth) > Node {
    if (depth > 0) {
        depth = depth - 1;

        return Node{
            left = bottomUpTree(depth),
            right = bottomUpTree(depth),
        };
    }

    return Node{};
}

fun itemCheck(Node tree) > int {
    if (tree.left != null) {
        return 1 + itemCheck(tree.left!) + itemCheck(tree.right!);
    } else {
        return 1;
    }

    return 0;
}

fun btree(int N) > void !> OverflowError, UnderflowError {
    int mindepth = 4;
    int maxdepth = mindepth + 2;
    if (maxdepth < N) {
        maxdepth = N;
    }

    int stretchdepth = maxdepth + 1;
    Node stretchtree = bottomUpTree(stretchdepth);
    print("stretch tree of depth {stretchdepth},\t check: {itemCheck(stretchtree)}");

    Node longlivedtree = bottomUpTree(maxdepth);
    for (int depth = mindepth; depth < maxdepth; depth = depth + 2) {
        int iterations = toInt(math.pow(x: 2.0, y: toFloat(maxdepth - depth + mindepth)));
        int check = 0;
        for (int i = 0; i < iterations; i = i + 1) {
            check = check + itemCheck(bottomUpTree(depth));
        }

        print("{iterations}\t trees of depth {depth}\t check: {check}");
    }

    print("long lived tree of depth {maxdepth}\t check: {itemCheck(longlivedtree)}");
}

fun main([str] args) > void !> OverflowError, UnderflowError {
    int N = 0;
    if (args.len() > 0) {
        N = parseInt(args[0]) ?? 0;
    }

    btree(N);
}