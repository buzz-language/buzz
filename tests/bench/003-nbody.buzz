import "std";
import "math";

final pi = 3.141592653589793;
final solarMass = 4.0 * pi * pi;
final daysPerYear = 365.24;

object Body {
    x: double,
    y: double,
    z: double,
    vx: double,
    vy: double,
    vz: double,
    mass: double,
}

fun advance(bodies: [mut Body], nbody: int, dt: double) > void {
    foreach (i in 0..nbody) {
        final bi = bodies[i];
        final bix = bi.x;
        final biy = bi.y;
        final biz = bi.z;
        final bimass = bi.mass;
        var bivx = bi.vx;
        var bivy = bi.vy;
        var bivz = bi.vz;

        foreach (j in (i + 1)..nbody) {
            final bj = bodies[j];
            final dx = bix - bj.x;
            final dy = biy - bj.y;
            final dz = biz - bj.z;
            final dist2 = dx * dx + dy * dy + dz * dz;
            var mag = math\sqrt(dist2);
            mag = dt / (mag * dist2);
            var bm = bj.mass * mag;

            bivx = bivx - (dx * bm);
            bivy = bivy - (dy * bm);
            bivz = bivz - (dz * bm);
            bm = bimass * mag;
            bj.vx = bj.vx + (dx * bm);
            bj.vy = bj.vy + (dy * bm);
            bj.vz = bj.vz + (dz * bm);
        }

        bi.vx = bivx;
        bi.vy = bivy;
        bi.vz = bivz;
        bi.x = bix + dt * bivx;
        bi.y = biy + dt * bivy;
        bi.z = biz + dt * bivz;
    }
}

fun energy(bodies: [Body], nbody: int) > double {
    var e = 0.0;
    foreach (i in 0..nbody) {
        final bi = bodies[i];
        final vx = bi.vx;
        final vy = bi.vy;
        final vz = bi.vz;
        final bim = bi.mass;
        e = e + (0.5 * bim * (vx * vx + vy * vy + vz * vz));
        foreach (j in (i + 1)..nbody) {
            final bj = bodies[j];
            final dx = bi.x - bj.x;
            final dy = bi.y - bj.y;
            final dz = bi.z - bj.z;
            final distance = math\sqrt(dx * dx + dy * dy + dz * dz);
            e = e - ((bim * bj.mass) / distance);
        }
    }

    return e;
}

fun offsetMomentum(b: [mut Body], nbody: int) > void {
    var px = 0.0;
    var py = 0.0;
    var pz = 0.0;

    foreach (i in 0..nbody) {
        final bi = b[i];
        final bim = bi.mass;

        px = px + (bi.vx * bim);
        py = py + (bi.vy * bim);
        pz = pz + (bi.vz * bim);
    }

    b[0].vx = -px / solarMass;
    b[0].vy = -py / solarMass;
    b[0].vz = -pz / solarMass;
}

fun main(args: [str]) > void {
    final sun = mut mut Body{
        x = 0.0,
        y = 0.0,
        z = 0.0,
        vx = 0.0,
        vy = 0.0,
        vz = 0.0,
        mass = solarMass,
    };

    final jupiter = mut Body{
        x = 4.84143144246472090,
        y = -1.16032004402742839,
        z = -0.103622044471123109,
        vx = 0.00166007664274403694 * daysPerYear,
        vy = 0.00769901118419740425 * daysPerYear,
        vz = -0.0000690460016972063023 * daysPerYear,
        mass = 0.000954791938424326609 * solarMass,
    };

    final saturn = mut Body{
        x = 8.34336671824457987,
        y = 4.12479856412430479,
        z = -0.403523417114321381,
        vx = -0.0027674251072686 * daysPerYear,
        vy = 0.0049985280123492 * daysPerYear,
        vz = 0.0000230417297573763929 * daysPerYear,
        mass = 0.00028588598066613 * solarMass,
    };

    final uranus = mut Body{
        x = 12.894369562139,
        y = -15.111151401699,
        z = -0.22330757889266,
        vx = 0.0029646013756476 * daysPerYear,
        vy = 0.0023784717395948 * daysPerYear,
        vz = -0.0000296589568540237556 * daysPerYear,
        mass = 0.0000436624404335156298 * solarMass,
    };

    final neptune = mut Body{
        x = 15.379697114851,
        y = -25.9193146099879641,
        z = 0.179258772950371181,
        vx = 0.00268067772490389322 * daysPerYear,
        vy = 0.00162824170038242295 * daysPerYear,
        vz = -0.0000951592254519715870 * daysPerYear,
        mass = 0.0000515138902046611451 * solarMass,
    };

    final bodies = [ sun, jupiter, saturn, uranus, neptune ];

    final N = std\parseInt(args[?0] ?? "1000") ?? 1000;
    final nbody = bodies.len();

    offsetMomentum(bodies, nbody: nbody);
    std\print("{energy(bodies, nbody: nbody)}");

    foreach (_ in 0..N) {
        advance(bodies, nbody: nbody, dt: 0.01);
    }

    std\print("{energy(bodies, nbody: nbody)}");
}
