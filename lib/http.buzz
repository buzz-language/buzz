import "lib/std";
import "lib/os";

object HttpParseError {
    str? message = null,
}

object Request {
    str method,
    {str, str} headers,
    str path,
    num contentLength = 0,
    str? body = null,

    fun toString() > str {
        str result = "{this.method} {this.path} HTTP/1.1\r\n";
        foreach (str key, str value in this.headers) {
            result = result + "{key}: {value}\r\n";
        }

        result = result + "{this.body ?? ""}\r\n";

        return result;
    }

    static fun parse(str raw) > Request !> HttpParseError, str {
        print(raw);
        [str] lines = raw.split("\n");
        [str]? methodAndUrl = _^(GET|POST|PUT|OPTIONS|PATCH) (.+) HTTP/1\.1_.match(lines[0]);

        if (methodAndUrl == null) {
            throw HttpParseError{ message = "Could not parse request method and path" };
        }

        str method = methodAndUrl![1];
        str path = methodAndUrl![2];

        {str, str} headers = <str, str>{};
        num contentLength = 0;
        num lineOffset = 0;
        foreach (num i, str line in lines.sub(1, len: null)) {
            if (line == "\n" or line == "\r" or line == "") {
                break;
            }

            lineOffset = lineOffset + 1;

            [str]? headerMatch = _^([^:]+): (.+)_.match(line);

            if (headerMatch == null) {
                throw HttpParseError{ message = "Could not parse request headers" };
            }

            headers[headerMatch![1]] = headerMatch![2];

            if (headerMatch![1] == "Content-Length" or headerMatch![1] == "content-length") {
                contentLength = parseNumber(headerMatch![2].trim()) ?? 0;
            }
        }

        str? body = null;
        if (contentLength > 0) {
            body = lines.sub(lineOffset + 2, len: null).join("\n");
        }

        return Request{
            method = method,
            path = path,
            headers = headers,
            contentLength = contentLength,
            body = body,
        };
    }
}

export Request;