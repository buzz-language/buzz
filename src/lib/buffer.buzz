import "errors";
import "ffi";
import "std";

export object WriteWhileReadingError {}
export object OutOfBoundError {}

|| @private
extern fun BufferNew(int capacity) > ud;
|| @private
extern fun BufferDeinit(ud userdata) > void;
|| @private
extern fun BufferRead(ud userdata, int n) > str?;
|| @private
extern fun BufferWrite(ud userdata, str bytes) > void !> WriteWhileReadingError;
|| @private
extern fun BufferReadBoolean(ud userdata) > bool?;
|| @private
extern fun BufferWriteBoolean(ud userdata, bool b) > void !> WriteWhileReadingError;
|| @private
extern fun BufferWriteInt(ud userdata, int n) > void !> WriteWhileReadingError;
|| @private
extern fun BufferReadInt(ud userdata) > int?;
|| @private
extern fun BufferWriteUserData(ud userdata, ud ptr) > void !> WriteWhileReadingError;
|| @private
extern fun BufferReadUserData(ud userdata) > ud?;
|| @private
extern fun BufferWriteFloat(ud userdata, float n) > void !> WriteWhileReadingError;
|| @private
extern fun BufferReadFloat(ud userdata) > float?;
|| @private
extern fun BufferLen(ud userdata, int align) > int;
|| @private
extern fun BufferCursor(ud userdata) > int;
|| @private
extern fun BufferBuffer(ud userdata) > str;
|| @private
extern fun BufferPtr(ud userdata, int at, int alignment) > ud;
|| @private
extern fun BufferEmpty(ud userdata) > void;
|| @private
extern fun BufferAt(ud userdata, int index) > int;
|| @private
extern fun BufferSetAt(ud userdata, int index, int value) > void;
|| @private
extern fun BufferWriteZ(ud userdata, str zigType, [any] values) > void !> WriteWhileReadingError, FFITypeMismatchError;
|| @private
extern fun BufferWriteZAt(ud userdata, int at, str zigType, [any] values) > void !> WriteWhileReadingError, FFITypeMismatchError;
|| @private
extern fun BufferReadZ(<T>, ud userdata, str zigType) > T !> FFITypeMismatchError;
|| @private
extern fun BufferReadZAt(<T>, ud userdata, int at, str zigType) > T !> FFITypeMismatchError;
|| @private
extern fun BufferWriteStruct(<T>, ud userdata, type structType, [T] values) > void !> WriteWhileReadingError, FFITypeMismatchError;
|| @private
extern fun BufferWriteStructAt(<T>, ud userdata, type structType, int at, [T] values) > void !> WriteWhileReadingError, FFITypeMismatchError;
|| @private
extern fun BufferReadStruct(<T>, ud userdata, type structType) > T !> FFITypeMismatchError;
|| @private
extern fun BufferReadStructAt(<T>, ud userdata, type structType, int at) > T !> FFITypeMismatchError;

|| Read and write data to a string buffer
export object Buffer {
    || @private
    ud buffer,
    || @private
    bool released = false,

    || @return A new `Buffer`
    static fun init(int capacity = 0) > Buffer {
        return Buffer{
            buffer = BufferNew(capacity)
        };
    }

    static fun fromStr(str string) > Buffer {
        Buffer buffer = Buffer.init();
        
        | We're sure we did not read this buffer before
        buffer.write(string) catch void;

        return buffer;
    }

    || Frees the buffer
    fun collect() > void {
        | Avoid double free
        if (!this.released) {
            this.released = true;
            BufferDeinit(this.buffer);
        }
    }

    || Reads `n` bytes
    || @return Read bytes or `null` if nothing to read
    fun read(int n = 1) > str? {
        return BufferRead(this.buffer, n: n);
    }

    || Writes a string
    || @param bytes Bytes to write
    || @hot
    fun write(str bytes) > void !> WriteWhileReadingError {
        BufferWrite(this.buffer, bytes: bytes);
    }

    fun writeZ(<T>, str zigType, [T] values) > void !> WriteWhileReadingError, FFITypeMismatchError {
        BufferWriteZ(this.buffer, zigType: zigType, values: values);
    }

    fun writeZAt(<T>, int at, str zigType, [T] values) > void !> WriteWhileReadingError, FFITypeMismatchError {
        BufferWriteZAt(this.buffer, at: at, zigType: zigType, values: values);
    }

    fun readZ(<T>, str zigType) > T !> FFITypeMismatchError {
        return BufferReadZ(<T>, this.buffer, zigType: zigType);
    }

    fun readZAt(<T>, int at, str zigType) > T !> FFITypeMismatchError {
        return BufferReadZAt(<T>, this.buffer, at: at, zigType: zigType);
    }

    fun writeStruct(<T>, type structType, [T] values) > void !> WriteWhileReadingError, FFITypeMismatchError {
        BufferWriteStruct(<T>, this.buffer, structType: structType, values: values);
    }

    fun writeStructAt(<T>, type structType, int at, [T] values) > void !> WriteWhileReadingError, FFITypeMismatchError {
        BufferWriteStructAt(<T>, this.buffer, structType: structType, at: at, values: values);
    }

    fun readStruct(<T>, type structType) > T !> FFITypeMismatchError {
        return BufferReadStruct(<T>, this.buffer, structType: structType);
    }

    fun readStructAt(<T>, type structType, int at) > T !> FFITypeMismatchError {
        return BufferReadStructAt(<T>, this.buffer, structType: structType, at: at);
    }

    || Reads a boolean
    || @return Boolean we read or `null` if nothing to read
    fun readBoolean() > bool? {
        return BufferReadBoolean(this.buffer);
    }

    || Writes a boolean
    || @param boolean Boolean to write
    fun writeBoolean(bool boolean) > void !> WriteWhileReadingError {
        BufferWriteBoolean(this.buffer, b: boolean);
    }

    || Reads an integer
    || @return Read integer or `null` if nothing to read
    fun readInt() > int? {
        return BufferReadInt(this.buffer);
    }

    || Writes an integer
    || @param number Integer to write
    fun writeInt(int number) > void !> WriteWhileReadingError {
        BufferWriteInt(this.buffer, n: number);
    }

    || Reads an ud
    || @return Read ud or `null` if nothing to read
    fun readUserData() > ud? {
        return BufferReadUserData(this.buffer);
    }

    || Writes an ud
    || @param number UserDataeger to write
    fun writeUserData(ud userdata) > void !> WriteWhileReadingError {
        BufferWriteUserData(this.buffer, ptr: userdata);
    }

    || Reads a float
    || @return Read float or `null` if nothing to read
    fun readFloat() > float? {
        return BufferReadFloat(this.buffer);
    }

    || Writes a float
    || @param number Float to write
    fun writeFloat(float number) > void !> WriteWhileReadingError {
        BufferWriteFloat(this.buffer, n: number);
    }


    || @return Length of the buffer
    fun len(int align = 1) > int {
        return BufferLen(this.buffer, align: align);
    }

    || @return Position of the reading cursor
    fun cursor() > int {
        return BufferCursor(this.buffer);
    }

    || Empties the buffer
    fun empty() > void {
        BufferEmpty(this.buffer);
    }

    || Get buffer as string
    fun toString() > str {
        return BufferBuffer(this.buffer);
    }

    || Get buffer's ptr
    fun ptr(int at = 0, int align = 1) > ud {
        return BufferPtr(this.buffer, at: at, alignment: align);
    }

    || Get byte at `index`
    fun at(int index) > int !> OutOfBoundError {
        if (index < this.len()) {
            return BufferAt(this.buffer, index: index);
        }

        throw OutOfBoundError{};
    }

    || Set byte at `index`
    fun setAt(int index, int value) > void !> WriteWhileReadingError, OutOfBoundError {
        if (index < this.len()) {
            BufferSetAt(this.buffer, index: index, value: value);

            return;
        }

        throw OutOfBoundError{};
    }
}