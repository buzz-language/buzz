import "errors";

|| @return epoch time in ms
export extern fun time() > float;

|| Returns environment variable under `key`
|| @param key environment variable name
export extern fun env(str key) > str? !> InvalidArgumentError;

|| @return path to system temp directory
export extern fun tmpDir() > str;

|| @param prefix prefix to the temp file name
|| @return a temporary file name in system tmp dir
export extern fun tmpFilename(str? prefix) > str;

|| Exit program with `exitCode`
|| @param exitCode exit code
export extern fun buzzExit(int exitCode) > void;

|| Execute command and return its exit code
|| @param command command to execute
|| @return exit code of the command
export extern fun execute([str] command) > int !> FileSystemError, UnexpectedError;

|| @private
extern fun SocketConnect(str address, int port, int netProtocol) > int !> InvalidArgumentError, SocketError, NotYetImplementedError;
|| @private
extern fun SocketClose(int fd) > void;
|| @private
extern fun SocketRead(int fd, int n) > str? !> InvalidArgumentError, FileSystemError, ReadWriteError, UnexpectedError;
|| @private
extern fun SocketWrite(int fd, str bytes) > void !> FileSystemError, ReadWriteError, UnexpectedError;
|| @private
extern fun SocketServerStart(str address, int port, bool reuseAddr, bool reusePort) > int !> InvalidArgumentError, SocketError, UnexpectedError, FileSystemError;
|| @private
extern fun SocketServerAccept(int fd, bool reuseAddr, bool reusePort) > int !> SocketError, UnexpectedError;
|| @private
extern fun SocketReadLine(int fd) > str? !> FileSystemError, UnexpectedError, ReadWriteError;
|| @private
extern fun SocketReadAll(int fd) > str? !> FileSystemError, UnexpectedError, ReadWriteError;

|| Protocols supported over a socket
export enum SocketProtocol {
    tcp,
    udp,
    ipc,
}

|| A socket
export object Socket {
    || @private
    int fd,

    || Opens a socket
    || @param address A string containing either a IPv4, IPv6 or path to a socket file (IPC)
    || @param port Port to which to connect
    || @param protocol Protocol to use
    || @return A new `Socket` opened and ready to use
    static fun connect(str address, int port = 0, SocketProtocol netProtocol) > Socket !> InvalidArgumentError, SocketError, NotYetImplementedError {
        return Socket{
            fd = SocketConnect(address, port: port, netProtocol: netProtocol.value),
        };
    }

    || Close the socket
    fun close() > void {
        SocketClose(this.fd);
    }

    || Receive at most `n` bytes from the socket
    || @param n How many bytes we're prepare to receive
    || @return The bytes received or null if nothing to read
    fun receive(int n) > str? !> InvalidArgumentError, FileSystemError, ReadWriteError, UnexpectedError {
        return SocketRead(this.fd, n: n);
    }

    || Receive from socket until it's closed or a linefeed is received
    || @return The bytes received or null if nothing to read
    fun receiveLine() > str? !> FileSystemError, UnexpectedError, ReadWriteError {
        return SocketReadLine(this.fd);
    }

    || Receive from socket until it's closed
    || @return The bytes received or null if nothing to read
    fun receiveAll() > str? !> FileSystemError, UnexpectedError, ReadWriteError {
        return SocketReadAll(this.fd);
    }

    || Send bytes on the socket
    || @param bytes Bytes to send
    fun send(str bytes) > void !> FileSystemError, ReadWriteError, UnexpectedError {
        SocketWrite(this.fd, bytes: bytes);
    }
}

|| A TCP Server
export object TcpServer {
    || @private
    Socket serverSocket,
    || @private
    bool reuseAddr,
    || @private
    bool reusePort,

    || Starts a TCP server
    || @param address Address to listen on
    || @param port Port to listen on
    || @param reuseAddr Wether we want to accept multiple connections
    || @param reusePort Wether we want to accept multiple connections
    || @return New `TcpServer` bound to `<address>:<port>`
    static fun init(str address, int port, bool reuseAddr, bool reusePort) > TcpServer !> SocketError, UnexpectedError, InvalidArgumentError, FileSystemError {
        return TcpServer{
            serverSocket = Socket{
                fd = SocketServerStart(address, port: port, reuseAddr: reuseAddr, reusePort: reusePort),
            },
            reuseAddr = reuseAddr,
            reusePort = reusePort,
        };
    }

    || Accept a new connection
    || @return Socket opened with the client
    fun accept() > Socket !> SocketError, UnexpectedError {
        return Socket{
            fd = SocketServerAccept(this.serverSocket.fd, reuseAddr: this.reuseAddr, reusePort: this.reusePort),
        };
    }

    || Close server
    fun close() > void {
        this.serverSocket.close();
    }
}

export SocketProtocol;
export Socket;
export TcpServer;